import org.apache.tools.ant.filters.ConcatFilter

plugins {
    id 'com.android.application'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.example.placesdemo"
        minSdkVersion 16
        targetSdkVersion 30
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions "version"
    productFlavors {
        gms {
            dimension "version"
            applicationIdSuffix ".gms"
            versionNameSuffix "-gms"
        }
        v3 {
            dimension "version"
            applicationIdSuffix ".v3"
            versionNameSuffix "-v3"
        }
    }
}

task generateV3(type: Copy) {
    group "V3 Beta"
    description "Copies source code from GMS to V3 BETA."

    from 'src/gms/java'
    into 'src/v3/java'
    filter { line ->
        line.replace('com.google.android.gms.maps', 'com.google.android.libraries.maps')
    }
    filter(ConcatFilter, prepend: file('../../V3_FILE_HEADER'))
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation "androidx.activity:activity:1.3.1"
    implementation "androidx.fragment:fragment:1.3.6"
    implementation 'com.android.volley:volley:1.2.1'
    implementation "com.github.bumptech.glide:glide:4.12.0"

    // GMS
    gmsImplementation 'com.google.android.libraries.places:places:2.5.0'
    gmsImplementation 'com.google.android.gms:play-services-maps:17.0.1'
    gmsImplementation 'com.google.maps.android:android-maps-utils:2.2.6'

    // V3
    v3Implementation name:'places-maps-sdk-3.1.0-beta', ext:'aar'
    v3Implementation 'com.google.android.gms:play-services-base:18.0.1'
    v3Implementation 'com.google.android.gms:play-services-basement:18.0.0'
    v3Implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    v3Implementation 'com.google.android.gms:play-services-location:19.0.1'
    v3Implementation 'com.google.android.gms:play-services-tasks:18.0.1'
    v3Implementation 'com.google.android.libraries.maps:maps:3.1.0-beta'
    v3Implementation 'com.google.auto.value:auto-value-annotations:1.9'
    v3Implementation 'com.google.code.gson:gson:2.8.9'
}

secrets {
    // To add your Google Maps Platform API key to this project:
    // 1. Create or open file local.properties in this folder, which will be ready by default
    //    by secrets_gradle_plugin
    // 2. Add this line, replacing YOUR_API_KEY with a key from a project with Places API enabled:
    //        PLACES_API_KEY=YOUR_API_KEY
    // 3. Add this line, replacing YOUR_API_KEY with a key from a project with Maps SDK for Android
    //    enabled (can be the same project and key as in Step 2):
    //        MAPS_API_KEY=YOUR_API_KEY
    defaultPropertiesFileName 'local.defaults.properties'
}