import org.apache.tools.ant.filters.ConcatFilter

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.example.placesdemo"
        minSdkVersion 16
        targetSdkVersion 29
        multiDexEnabled true
        versionCode 1
        versionName "1.0"

        // Read the API key from ./secure.properties into R.string.maps_api_key
        def secureProps = new Properties()
        if (file("../../secure.properties").exists()) {
            file("../../secure.properties")?.withInputStream { secureProps.load(it) }
        }
        buildConfigField("String", "PLACES_API_KEY", (secureProps.getProperty("PLACES_API_KEY") ?: "\"\""))

        // To add your Maps API key to this project:
        // 1. Create a file ./secure.properties
        // 2. Add this line, where YOUR_API_KEY is your API key:
        //        PLACES_API_KEY=YOUR_API_KEY
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions "version"
    productFlavors {
        gms {
            dimension "version"
            applicationIdSuffix ".gms"
            versionNameSuffix "-gms"
        }
        v3 {
            dimension "version"
            applicationIdSuffix ".v3"
            versionNameSuffix "-v3"
        }
    }
}

task generateV3(type: Copy) {
    group "V3 Beta"
    description "Copies source code from GMS to V3 BETA."

    from 'src/gms/java'
    into 'src/v3/java'
    filter { line ->
        line.replace('com.google.android.gms.maps', 'com.google.android.libraries.maps')
    }
    filter(ConcatFilter, prepend: file('../../V3_FILE_HEADER'))
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.material:material:1.2.1'

    // GMS
    gmsImplementation 'com.google.android.libraries.places:places:2.4.0'

    // V3
    v3Implementation name:'places-maps-sdk-3.1.0-beta', ext:'aar'
    v3Implementation 'com.android.volley:volley:1.1.1'
    v3Implementation 'com.google.android.gms:play-services-base:17.5.0'
    v3Implementation 'com.google.android.gms:play-services-basement:17.5.0'
    v3Implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    v3Implementation 'com.google.android.gms:play-services-location:17.1.0'
    v3Implementation 'com.google.android.gms:play-services-tasks:17.2.1'
    v3Implementation 'com.google.android.libraries.maps:maps:3.1.0-beta'
    v3Implementation 'com.google.auto.value:auto-value-annotations:1.7.4'
    v3Implementation 'com.google.code.gson:gson:2.8.6'
}
